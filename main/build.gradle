apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'  //for greenDao

import java.text.DateFormat
import java.text.SimpleDateFormat

def getDate() {
    DateFormat df = new SimpleDateFormat("yyyyMMdd");
    return df.format(new Date());
}


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.zenglb.framework"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

//    signingConfigs {
//        MyConfig {
//            storeFile file('E:/Android/Code/AnyLife_Zlb_Keystore')
//
//            keyAlias properties.getProperty("MyConfig.user")
//            keyPassword properties.getProperty("MyConfig.password")
//            storePassword properties.getProperty("MyConfig.password")
//        }
//    }

    buildTypes {
        release {
            minifyEnabled true    //proguard
            zipAlignEnabled true  // Zipalign优化
            shrinkResources true  // 移除无用的resource文件,必须和minifyEnabled 一起使用
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
//            signingConfig signingConfigs.MyConfig
            manifestPlaceholders = [APP_DEBUG: false, APP_NAME: "@string/app_name", API_VERSION: getDate()]
        }

        debug {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable true
//            signingConfig signingConfigs.MyConfig
            manifestPlaceholders = [APP_DEBUG: true, APP_NAME: "@string/app_name", API_VERSION: getDate()]
        }

        applicationVariants.all { variant ->
            if (variant.buildType.name == "release") {
                variant.outputs.each { output ->
                    output.outputFile = new File(
                            output.outputFile.parent,
                            output.outputFile.name.replace(".apk", "-${variant.versionName}.apk"))
                }
            }

            variant.assemble.doLast {
                variant.outputs.each { output ->
                    println "aligned " + output.outputFile
                    println "unaligned " + output.packageApplication.outputFile

                    File unaligned = output.packageApplication.outputFile;
                    File aligned = output.outputFile
                    if (!unaligned.getName().equalsIgnoreCase(aligned.getName())) {
                        println "deleting " + unaligned.getName()
                        unaligned.delete()
                    }
                }
            }
        }//applicationVariants.all  is over
    }

    greendao {
        schemaVersion 4                  //修改这里的版本号，greenDAO会自动修改生成到DAOMaster中的版本号
        targetGenDir 'src/main/java'     //表示greenDAO生成的DAOMaster和DaoSession的位置
        daoPackage 'com.zenglb.framework.database.daomaster' //我觉得最好放在Entity 下面的目录！！！
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile project(':commonLib')
    compile project(':settingViewLib')

    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:support-v4:25.2.0'
    compile 'com.android.support:cardview-v7:25.2.0'

    //1.greemdao 和加密
    compile 'org.greenrobot:greendao:3.2.0'
    compile 'net.zetetic:android-database-sqlcipher:3.5.4'
    //2.Recyclerview 动画和分割线
    compile 'jp.wasabeef:recyclerview-animators:2.2.4'
    compile 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    //3.上拉下拉加载数据
    compile 'com.liaoinstan.springview:library:1.2.6'
    //4.http 网络数据处理
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1' //
    compile 'com.google.code.gson:gson:2.4'
    //5.百分比布局控件
    compile 'com.zhy:percent-support-extends:1.1.1'
    //6.JSBRIDGE

    //7.zxing
    compile 'com.google.zxing:core:3.2.1'
    compile 'com.journeyapps:zxing-android-embedded:3.4.0'


}
